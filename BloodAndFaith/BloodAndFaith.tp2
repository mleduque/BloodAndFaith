BACKUP ~weidu_external/BloodAndFaith/backup~
AUTHOR ~Glittergear~
VERSION ~1.3~


ALWAYS

	ACTION_IF GAME_IS ~bgee~ BEGIN

		INCLUDE ~BloodAndFaith/lib/g3_bgee_cpmvars.tpa~
		PRINT @1 /* Game is bgee */

	END ELSE ACTION_IF GAME_IS ~eet~ BEGIN

		INCLUDE ~eet/other/cpmvars/eet_cpmvars.tpa~
		PRINT @2 /* Game is EET */


	END ELSE ACTION_IF GAME_IS ~bgt~ BEGIN

		INCLUDE ~BloodAndFaith/lib/g3_bgt_cpmvars.tpa~
		PRINT @3 /* Game is BGT */

	END

    //Copies tra files into the autotra-folder (to leave the originals untouched)
    DEFINE_ACTION_FUNCTION autotra_workaround BEGIN
      COPY ~BloodAndFaith/Languages/english~			~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%~
      COPY ~BloodAndFaith/Languages/english/Dlg~		~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%/Dlg~
      COPY ~BloodAndFaith/Languages/%LANGUAGE%~			~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%~
      COPY ~BloodAndFaith/Languages/%LANGUAGE%/Dlg~		~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%/Dlg~
    END

    LAF autotra_workaround END

// Convert tra files in UTF8 if EE and let them in ANSI if oBG

	ACTION_IF GAME_IS ~bgee eet~ BEGIN
  ACTION_DEFINE_ARRAY BloodAndFaith_noconvert BEGIN install END

		ACTION_DEFINE_ARRAY BloodAndFaith_reload BEGIN BloodAndFaith JournalEntries END

			LAF HANDLE_CHARSETS
				INT_VAR
				infer_charsets = 1
				STR_VAR
				tra_path = EVAL ~weidu_external/BloodAndFaith/Languages/autotra~
				out_path = EVAL ~weidu_external/BloodAndFaith/Languages/autotra/UTF8~
				iconv_path = ~BloodAndFaith/Languages/autotra/iconv~
				 noconvert_array = BloodAndFaith_noconvert
				reload_array = BloodAndFaith_reload
			END

	// UTF8 only
	COPY ~weidu_external/BloodAndFaith/Languages/autotra/UTF8/%LANGUAGE%~				~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%~
	COPY ~weidu_external/BloodAndFaith/Languages/autotra/UTF8/%LANGUAGE%/Dlg~			~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%/Dlg~
	END

END

AUTO_TRA ~weidu_external/BloodAndFaith/Languages/autotra/%s~ //The "%s" reads in the chosen language folder via LANGUAGE. Currently there is only one.
AUTO_TRA ~weidu_external/BloodAndFaith/Languages/autotra/%s/Dlg~


LANGUAGE ~English~
         ~english~
         ~BloodAndFaith/Languages/english/BloodAndFaith.tra~ //you'd put the mod title in here.
         ~BloodAndFaith/Languages/english/JournalEntries.tra~

LANGUAGE ~Simplified Chinese(by Lzw104522773)~
         ~schinese~
         ~BloodAndFaith/Languages/english/BloodAndFaith.tra~
         ~BloodAndFaith/Languages/schinese/BloodAndFaith.tra~ //you'd put the mod title in here.
         ~BloodAndFaith/Languages/english/JournalEntries.tra~
         ~BloodAndFaith/Languages/schinese/JournalEntries.tra~
         ~BloodAndFaith/Languages/schinese/install.tra~

LANGUAGE ~Russian (by CblblP & Arcanecoast.ru)~
         ~russian~
         ~BloodAndFaith/Languages/english/BloodAndFaith.tra~
         ~BloodAndFaith/Languages/russian/BloodAndFaith.tra~ //you'd put the mod title in here.
         ~BloodAndFaith/Languages/english/JournalEntries.tra~
         ~BloodAndFaith/Languages/russian/JournalEntries.tra~


BEGIN @4 /* Blood and Faith Installation */
DESIGNATED 0
LABEL ~Blood-And-Faith-Glittergear~
REQUIRE_PREDICATE GAME_IS ~bgee eet bgt~ @42 /* This mod need BGEE, BGT or EET */

ADD_JOURNAL TITLE (@5099) @5100 @5101 @5102 @5103 @5104 @5105 @5106 @5107 @5108 @5109 @5110 @5111 @5113  @5116 @5117 @5118 @5119 @5120 @5121 @5122 @5123 @5124 @5125 @5126 @5127 @5128 @5129 @5130 @5131 @5132 @5133 @5134 @5135 @5136 @5137 @5138 @5139 @5140 @5141 @5142 USING ~weidu_external/BloodAndFaith/Languages/autotra/%s/JournalEntries.tra~

//Portraits

COPY ~BloodAndFaith/portraits/all~ ~override~

LAF HANDLE_AUDIO END
ADD_MUSIC ~g_bff~ ~%MOD_FOLDER%/music/g_bff.mus~
ADD_MUSIC ~g3blank~ ~%MOD_FOLDER%/music/g3blank.mus~

//COPY ~%MOD_FOLDER%/music/g_bffa.acm~ ~MUSIC/g_bff/g_bffa.acm~
COPY ~%MOD_FOLDER%/music/g_bff.ogg~ ~MUSIC/g_bff/g_bff.ogg~
COPY ~%MOD_FOLDER%/music/g_bff.ogg~ ~MUSIC/g_bff/g_bff.ogg~
COPY ~%MOD_FOLDER%/music/blanka.acm~ ~MUSIC/g_bff/blanka.acm~
COPY ~%MOD_FOLDER%/music/blank.wav~ ~MUSIC/g_bff/blank.wav~


COPY_EXISTING ~songlist.2da~ ~override~
    SET_2DA_ENTRY 0 2 3 ~g_blank.mus~

//spawns
EXTEND_TOP ~%NBaldursGate_TempleofHelm_BCS%.bcs~ ~BloodAndFaith/Scripts/AR0002.baf~ //spawns arty
EXTEND_TOP ~%BaldursGate_Undercellars_BCS%.bcs~ ~BloodAndFaith/Scripts/AR0112.baf~ //mordy
EXTEND_TOP ~%ESewers_BCS%.bcs~ ~BloodAndFaith/Scripts/AR0226.baf~ //doppelganger
EXTEND_TOP ~%EBaldursGate_SilencesShop_BCS%.bcs~ ~BloodAndFaith/Scripts/AR0809.baf~ //spawns loviatan
EXTEND_TOP ~%BanditCamp_BCS%.bcs~ ~BloodAndFaith/Scripts/AR1900.baf~ //spawns ex-thief
EXTEND_TOP ~%CloakwoodNest_BCS%.bcs~ ~BloodAndFaith/Scripts/AR2100.baf~ //spawns cloakwood
EXTEND_TOP ~%CoastWay_BCS%.bcs~ ~BloodAndFaith/Scripts/AR2800.baf~ //coastway zhent
EXTEND_TOP ~g_warren.bcs~ ~BloodAndFaith/Scripts/g_warren.baf~
EXTEND_TOP ~%UlcasterRuins_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3901.baf~ //spawns necro
EXTEND_TOP ~%Beregost_House12_L1_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3311.baf~
EXTEND_TOP ~%Beregost_House12_L2_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3312.baf~
EXTEND_TOP ~%Beregost_House23_L1_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3325.baf~
EXTEND_TOP ~%Beregost_House23_L2_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3326.baf~
EXTEND_TOP ~%Beregost_RedSheaf_L1_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3357.baf~
EXTEND_TOP ~%Temple_SongoftheMorning_BCS%.bcs~ ~BloodAndFaith/Scripts/AR3402.baf~
EXTEND_TOP ~%Nashkel_BCS%.bcs~ ~BloodAndFaith/Scripts/AR4800.baf~
EXTEND_TOP ~%Nashkel_TempleofHelm_BCS%.bcs~ ~BloodAndFaith/Scripts/AR4802.baf~
EXTEND_TOP ~%LonelyPeaks_BCS%.bcs~ ~BloodAndFaith/Scripts/AR4400.baf~ //spawns cannibal

	COPY_EXISTING ~FTOBE3.CRE~ ~override/FTOBE3.CRE~
		WRITE_ASCII DEATHVAR ~FTOBE3~ (32) // Female Townsfolk Beregost_House12_L1_BCS

	COPY_EXISTING ~MTBE8.CRE~ ~override/MTBE8.CRE~
		WRITE_ASCII DEATHVAR ~MTBE8~ (32) // Male Townsfolk Beregost_House23_L1_BCS

//scripts
COMPILE ~BloodAndFaith/Scripts/g_BFat.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFat2.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFat3.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFmal.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFmalF.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFmorC.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFmord.baf~
COMPILE ~BloodAndFaith/Scripts/g_BFmorj.baf~

COPY ~%MOD_FOLDER%/spells/g_bfci.spl~         ~override~


//dialogues
COMPILE ~BloodAndFaith/dialogues/d_BF3gk.d~
COMPILE ~BloodAndFaith/dialogues/d_BF3z.d~
COMPILE ~BloodAndFaith/dialogues/d_BF9ov.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFgh.d~
COMPILE ~BloodAndFaith/dialogues/d_gBF1w.d~
COMPILE ~BloodAndFaith/dialogues/d_gBF1ho.d~
COMPILE ~BloodAndFaith/dialogues/d_gBF4ho.d~
COMPILE ~BloodAndFaith/dialogues/d_gBF4rk.d~
COMPILE ~BloodAndFaith/dialogues/d_gBF7st.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFart.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFdop.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFdrZ.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFlov.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFmor.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFmorj.d~
COMPILE EVALUATE_BUFFER ~BloodAndFaith/dialogues/d_gBFmqg.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFzh1.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFzl.d~
COMPILE ~BloodAndFaith/dialogues/d_gBFmal.d~

	WITH_TRA ~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%/Dlg/d_gBF1ho.TRA~
			 ~weidu_external/BloodAndFaith/Languages/autotra/%LANGUAGE%/Dlg/d_gBF4ho.TRA~ BEGIN
		COMPILE ~BloodAndFaith/dialogues/d_gDeactivate.d~
	END


//creatures
COPY ~BloodAndFaith/creatures/g_bart.cre~ ~override/g_bart.cre~
	SAY NAME1 @5 /* Arteger */
	SAY NAME2 @5 /* Arteger */

COPY ~BloodAndFaith/creatures/g_bfgho.cre~ ~override/g_bfgho.cre~
	SAY NAME1 @6 /* Ghost */
	SAY NAME2 @6 /* Ghost */

COPY ~BloodAndFaith/creatures/g_bfgra.cre~ ~override/g_bfgra.cre~ //
	SAY NAME1 @7 /* Gravekeeper */
	SAY NAME2 @7 /* Gravekeeper */

COPY ~BloodAndFaith/creatures/g_bfhom.cre~ ~override/g_bfhom.cre~ //
	SAY NAME1 @8 /* Homeowner */
	SAY NAME2 @8 /* Homeowner */

COPY ~BloodAndFaith/creatures/g_bfhow.cre~ ~override/g_bfhow.cre~ //
	SAY NAME1 @8 /* Homeowner */
	SAY NAME2 @8 /* Homeowner */

COPY ~BloodAndFaith/creatures/g_bfovl.cre~ ~override/g_bfovl.cre~ //
	SAY NAME1 @9 /* Ovlin */
	SAY NAME2 @9 /* Ovlin */

// Missing COMPB05.itm replaced by BOW01.itm for BGT
		ACTION_IF GAME_IS ~bgt~ BEGIN
COPY_EXISTING ~g_bfovl.cre~ ~override/g_bfovl.cre~
	REPLACE_CRE_ITEM ~BOW01~ #0 #0 #0 ~none~ ~weapon2~ EQUIP
		END

COPY ~BloodAndFaith/creatures/g_bfre.cre~ ~override/g_bfre.cre~ //
	SAY NAME1 @10 /* Officer Reatin */
	SAY NAME2 @10 /* Officer Reatin */
WRITE_ASCII SCRIPT_RACE ~~ (8) // Unused script

// Remove missing UNIFLAM.itm for BGT
		ACTION_IF GAME_IS ~bgt~ BEGIN
COPY_EXISTING ~g_bfre.cre~ ~override/g_bfre.cre~
	REMOVE_CRE_ITEM ~UNIFLAM~ ~cloak~
		END

COPY ~BloodAndFaith/creatures/g_bfwi.cre~ ~override/g_bfwi.cre~ //
	SAY NAME1 @11 /* Widow */
	SAY NAME2 @11 /* Widow */

COPY ~BloodAndFaith/creatures/g_bfzom.cre~ ~override/g_bfzom.cre~ //
	SAY NAME1 @12 /* Friendly Zom */
	SAY NAME2 @12 /* Friendly Zom */

COPY ~BloodAndFaith/creatures/g_bZhent.cre~ ~override/g_bZhent.cre~ //
	SAY NAME1 @13 /* Evil Zhent */
	SAY NAME2 @13 /* Evil Zhent */

COPY ~BloodAndFaith/creatures/g_bZhen2.cre~ ~override/g_bZhen2.cre~ //
	SAY NAME1 @13 /* Evil Zhent */
	SAY NAME2 @13 /* Evil Zhent */

COPY ~BloodAndFaith/creatures/g_bZhen3.cre~ ~override/g_bZhen3.cre~ //
	SAY NAME1 @13 /* Evil Zhent */
	SAY NAME2 @13 /* Evil Zhent */

COPY ~BloodAndFaith/creatures/g_bfcle.cre~ ~override/g_bfcle.cre~ //
	SAY NAME1 @13 /* Evil Zhent */
	SAY NAME2 @13 /* Evil Zhent */

COPY ~BloodAndFaith/creatures/g_dop.cre~ ~override/g_dop.cre~ //
	SAY NAME1 @14 /* Friendly Dop */
	SAY NAME2 @14 /* Friendly Dop */

COPY ~BloodAndFaith/creatures/g_lovi.cre~ ~override/g_lovi.cre~ //
	SAY NAME1 @15 /* The Loviatan */
	SAY NAME2 @15 /* The Loviatan */

COPY ~BloodAndFaith/creatures/g_mordy.cre~ ~override/g_mordy.cre~ //
	SAY NAME1 @16 /* Mordiyin */
	SAY NAME2 @16 /* Mordiyin */
	SAY BATTLE_CRY1 ~~ [g_bma1]
	SAY BATTLE_CRY2 ~~ [g_bma2]
	SAY BATTLE_CRY3 ~~ [g_bma3]
	SAY BATTLE_CRY4 ~~ [g_bma4]
	SAY BATTLE_CRY5 ~~
	SAY DAMAGE ~~ [g_bmou]
	SAY DYING ~~ [g_bmde]

COPY ~BloodAndFaith/creatures/g_mordyj.cre~ ~override/g_mordyj.cre~ //
	SAY NAME1 @16 /* Mordiyin */
	SAY NAME2 @16 /* Mordiyin */
	SAY BATTLE_CRY1 ~~ [g_bma1]
	SAY BATTLE_CRY2 ~~ [g_bma2]
	SAY BATTLE_CRY3 ~~ [g_bma3]
	SAY BATTLE_CRY4 ~~ [g_bma4]
	SAY BATTLE_CRY5 ~~
	SAY DAMAGE ~~ [g_bmou]
	SAY DYING ~~ [g_bmde]

COPY ~BloodAndFaith/creatures/g_necro.cre~ ~override/g_necro.cre~ //
	SAY NAME1 @17 /* Necromancer */
	SAY NAME2 @17 /* Necromancer */

COPY ~BloodAndFaith/creatures/g_rat1.cre~ ~override/g_rat1.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */

COPY ~BloodAndFaith/creatures/g_rat2.cre~ ~override/g_rat2.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */

COPY ~BloodAndFaith/creatures/g_rat3.cre~ ~override/g_rat3.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */
	WRITE_ASCII 0x280 ~g_rat3~ (32) // Death var

COPY ~BloodAndFaith/creatures/g_rat4.cre~ ~override/g_rat4.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */

COPY ~BloodAndFaith/creatures/g_rat5.cre~ ~override/g_rat5.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */

COPY ~BloodAndFaith/creatures/g_rat6.cre~ ~override/g_rat6.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */

COPY ~BloodAndFaith/creatures/g_rat7.cre~ ~override/g_rat7.cre~ //
	SAY NAME1 @18 /* Rat */
	SAY NAME2 @18 /* Rat */

ACTION_IF GAME_IS ~bgee eet~ AND NOT FILE_EXISTS_IN_GAME ~mwraa1.bam~ BEGIN
	COPY ~BloodAndFaith/animations/wererat~ ~override~
END

COPY ~BloodAndFaith/creatures/g_rk.cre~ ~override/g_rk.cre~ //
	SAY NAME1 @19 /* The Rat King */
	SAY NAME2 @19 /* The Rat King */
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~WERERAT~))
	END

COPY ~BloodAndFaith/creatures/g_rZhent.cre~ ~override/g_rZhent.cre~ //
	SAY NAME1 @20 /* Renegade */
	SAY NAME2 @20 /* Renegade */

COPY ~BloodAndFaith/creatures/g_thief.cre~ ~override/g_thief.cre~ //
	SAY NAME1 @21 /* Ex-Thief */
	SAY NAME2 @21 /* Ex-Thief */

COPY ~BloodAndFaith/creatures/g_zhent1.cre~ ~override/g_zhent1.cre~ //
	SAY NAME1 @22 /* Zhent Agent */
	SAY NAME2 @22 /* Zhent Agent */

ACTION_IF GAME_IS ~bgee eet~ AND NOT FILE_EXISTS_IN_GAME ~mmala1.bam~ BEGIN
	COPY ~BloodAndFaith/animations/malarite~ ~override~
END

COPY ~BloodAndFaith/creatures/gg_malar.cre~ ~override/gg_malar.cre~ //
	SAY NAME1 @23 /* Malarite */
	SAY NAME2 @23 /* Malarite */
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~MALARITE~))
	END

COPY ~BloodAndFaith/creatures/gg_malaF.cre~ ~override/gg_malaF.cre~ //
	SAY NAME1 @23 /* Malarite */
	SAY NAME2 @23 /* Malarite */
	WRITE_ASCII DIALOG ~d_gBFMal~ (8)
	PATCH_IF GAME_IS ~bgee eet~ BEGIN
		WRITE_LONG 0x28 (IDS_OF_SYMBOL (~animate~ ~MALARITE~))
	END

COPY ~BloodAndFaith/creatures/gg_can.cre~ ~override/gg_can.cre~ //
	SAY NAME1 @24 /* Lenovela the Cannibal */
	SAY NAME2 @24 /* Lenovela the Cannibal */

COPY ~BloodAndFaith/creatures/g_eloran.cre~ ~override/g_eloran.cre~ //
	SAY NAME1 @25 /* Eloran */
	SAY NAME2 @25 /* Eloran */

COPY ~BloodAndFaith/items/gBFcc.itm~ ~override/gBFcc.itm~
     SAY NAME1 @26 /* Coin Collection */
     SAY NAME2 @26 /* Coin Collection */
     SAY DESC @27 /* This coin collection doesn’t have much value, except to the most avid of collectors. */

COPY ~BloodAndFaith/items/gBFed.itm~ ~override/gBFed.itm~
    SAY NAME1 @28 /* Encoded Documents */
    SAY NAME2 @28 /* Encoded Documents */
    SAY DESC @29 /* This documents are encoded in a way that you've never seen before. */

// V1.2 items

COPY ~BloodAndFaith/items/g_bfiMs.itm~ ~override/g_bfiMs.itm~
    SAY NAME1 @31 /* ~Mordiyin's Sword~ */
    SAY NAME2 @31 /* ~Mordiyin's Sword~ */
	SAY UNIDENTIFIED_DESC @32 /* ~Something seems off about this sword.~ */
    SAY DESC @33 /* ~Taken from Mordiyin, a Zhent agent who specialized in hunting down defectors, this is a thoroughly evil weapon. Over the years, it has tasted the blood of many, and it thirsts for battle, for it is unnervingly proficient when wielded against humanoid foes. It glows with a chill light, and anyone not of evil intent finds that its touch burns too much to wield it.

STATISTICS:

THAC0: +1, +3 vs. humanoid
Damage: 1d8+1, +3 vs. humanoid
Damage type: Slashing
Speed Factor: 4
Proficiency Type: Long Sword
Type: One-handed
Requires:
 6 Strength
 Cannot be used by Good-aligned character

Weight: 4 ~ */

COPY ~BloodAndFaith/items/g_bfcl.itm~ ~override/g_bfcl.itm~
    SAY NAME1 @34 /* ~Beast Claw of Malar~ */
    SAY NAME2 @34 /* ~Beast Claw of Malar~ */
    SAY DESC @35 /* ~ Said to be a claw taken from one of Malar's favored beasts, this ring reeks of blood and pulsates slightly to the touch. The wearer is imbued with the Beastlord's stealth and ferocity, but at the cost of <PRO_HISHER> judgment and social graces.


Equipped abilities:
– Armor Class: +1, an extra +4 vs. missile attacks
– +10% Hide in Shadows
– +10% Move Silently
- +2 Speed Modifier
– -2 Wisdom
- -3 Charisma
– May only be removed with a Remove Curse spell~*/


COPY ~BloodAndFaith/items/g_bfist.itm~ ~override/g_bfist.itm~
    SAY NAME1 @36 /* ~Spinal Staff~ */
    SAY NAME2 @36 /* ~Spinal Staff~ */
    SAY DESC @37 /* ~The remains of some necromancer's victim, a trace of the agony felt by the spine's original owner can still be felt. It renders the bearer repellent in the eyes of others, but the magical power it grants is difficult to argue with. Sometimes it shifts in the hand as though still alive.


Equipped abilities:
– THAC0 +1
– Double wizard spells
- -3 Charisma
- -2 Constitution
- Can't be used by Good ~*/

COPY ~BloodAndFaith/items/g_bfiam.itm~ ~override/g_bfiam.itm~
    SAY NAME1 @38 /* ~Eloran's Amulet~ */
    SAY NAME2 @38 /* ~Eloran's Amulet~ */
    SAY DESC @39 /* ~Emblazoned with the Morninglord's symbol, this gold amulet grants its wearer greater attention in the eyes of <PRO_HISHER> god. It cannot be used by those who serve evil.

Equipped abilities:
– Double Divine Spellslots
- Only usable by clerics
- Can't be used by Evil ~ */

COPY ~BloodAndFaith/items/g_bfci.itm~ ~override/g_bfci.itm~
    SAY NAME1 @40 /* ~Cloak of Ibrandul~ */
    SAY NAME2 @40 /* ~Cloak of Ibrandul~ */
    SAY DESC @41 /* ~Once worn by a follower of Ibrandul, the god of caverns, the cloak has taken on something of his darkness. Featherlight, it twists like a living shadow, and confers greater concealment at night.

Equipped abilities:
- AC +1
- Infravision
- Move silently +10 (active at night)
- Hide in Shadows +10 (active at night)~ */


	ACTION_IF GAME_IS ~bgee~ BEGIN
OUTER_SPRINT "spider" "SPIDGI"
	END ELSE BEGIN
OUTER_SPRINT "spider" "SPIDGI01"
	END

COPY ~BloodAndFaith/Areas/g_warren.are~ ~override~
	WRITE_ASCIIE 0x08 ~%WerewolfCaverns%~ (8)
	LPF ALTER_AREA_REGION
		STR_VAR
		region_name              = Door2001
		destination_area = EVAL ~%CoastWay%~
	END
// Redo spawn points
	LPF fj_are_structure
		INT_VAR
		fj_loc_x = 344
		fj_loc_y = 1115
		fj_spawn_num = 1
		fj_difficulty = 200
		fj_delay = 6
		fj_method = BIT0
		fj_duration = 1000
		fj_wander_dist_spawn = 1000
		fj_mvmt_dist_spawn = 1000
		fj_max_num = 2
		STR_VAR
		fj_structure_type = Spawn
		fj_name           = g_Spawn_Point_1
		fj_cre_resref0    = EVAL ~%spider%~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x = 1521
		fj_loc_y = 832
		fj_spawn_num = 1
		fj_difficulty = 200
		fj_delay = 6
		fj_method = BIT0
		fj_duration = 1000
		fj_wander_dist_spawn = 1000
		fj_mvmt_dist_spawn = 1000
		fj_max_num = 2
		STR_VAR
		fj_structure_type = Spawn
		fj_name           = g_Spawn_Point_2
		fj_cre_resref0    = EVAL ~%spider%~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x = 2868
		fj_loc_y = 850
		fj_spawn_num = 1
		fj_difficulty = 200
		fj_delay = 6
		fj_method = BIT0
		fj_duration = 1000
		fj_wander_dist_spawn = 1000
		fj_mvmt_dist_spawn = 1000
		fj_max_num = 2
		STR_VAR
		fj_structure_type = Spawn
		fj_name           = g_Spawn_Point_3
		fj_cre_resref0    = EVAL ~%spider%~
	END
	LPF fj_are_structure
		INT_VAR
		fj_loc_x = 271
		fj_loc_y = 493
		fj_spawn_num = 1
		fj_difficulty = 200
		fj_delay = 6
		fj_method = BIT0
		fj_duration = 1000
		fj_wander_dist_spawn = 1000
		fj_mvmt_dist_spawn = 1000
		fj_max_num = 2
		STR_VAR
		fj_structure_type = Spawn
		fj_name           = g_Spawn_Point_6
		fj_cre_resref0    = EVAL ~%spider%~
	END


COPY_EXISTING ~%CoastWay%.are~ ~override~
  LPF fj_are_structure
    INT_VAR
    fj_type         = 2    //travel
    fj_box_left     = 2523
    fj_box_top      = 3130
    fj_box_right    = 2638
    fj_box_bottom   = 3211
    fj_cursor_idx   = 30   // door symbol
    fj_vertex_1    = 2638  + (3168 << 16)
    fj_vertex_2    	= 2524  + (3140  << 16)
    fj_vertex_3     = 2523  + (3201  << 16)
    fj_vertex_0   = 2637  + (3211 << 16)
    STR_VAR
    fj_structure_type   = region
    fj_name             = Tran_G_WAREnter // name of this travel region so you can address it via script etc.
    fj_destination_area = g_warren	// *.ARE
    fj_destination_name = Exit2001 // name of exitpoint in destination area
  END

//exit point for back travel
  LPF fj_are_structure
    INT_VAR
    fj_loc_x       = 2570
    fj_loc_y       = 3250
    fj_orientation = 6   // NW
    STR_VAR
    fj_structure_type = entrance
    fj_name           = Tran_G_WAROut
  END

	ADD_MAP_NOTE #2617 #3161 ~violet~ @30 /* Warrens */
